apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'

apply from: "scripts/config.gradle"
apply from: "scripts/dependencies.gradle"
def configuration = ext.configuration

realm {
    syncEnabled = true
    kotlinExtensionsEnabled = true
}
android {
    compileSdkVersion configuration.compileSdkVersion
    defaultConfig {
        applicationId configuration.applicationId
        minSdkVersion configuration.minSdkVersion
        targetSdkVersion configuration.targetSdkVersion
        versionCode configuration.versionCode
        versionName configuration.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    flavorDimensions "client"
    productFlavors {
        demo {
            dimension "client"
            applicationIdSuffix ".demo"
        }
        full {
            dimension "client"
            applicationIdSuffix ".full"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            applicationIdSuffix ".release"
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                def buildTypeName = variant.buildType.name
                def flavorTypeName = variant.flavorName
                outputFileName =
                        "Sample Application-${flavorTypeName}-${buildTypeName}-v${variant.versionName}(${android.defaultConfig.versionCode}).apk"
            }
        }
    }

}

dependencies {
    implementation 'com.android.support:support-v4:27.+'
}
